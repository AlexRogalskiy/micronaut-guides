import groovy.json.JsonSlurper
import io.micronaut.guides.GenerateGuideZip
import io.micronaut.guides.GuideAsciidocGenerator
import io.micronaut.guides.GuideMetadata
import io.micronaut.guides.GuideProjectGenerator

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'io.micronaut.docs', name: 'micronaut-docs-asciidoc-extensions', version: '1.0.25'
    }
}

plugins {
    id 'groovy'
    id("org.asciidoctor.jvm.convert") version "3.3.0"
}

apply from: "gradle/asciidoc.gradle"

repositories {
    mavenCentral()
    jcenter()
}

task cleanAsciidoctorFolder {
    doLast {
        File f = new File('src/docs/asciidoc')
        if (f.exists()) {
            f.deleteDir()
        }
    }
}
task copyImagesToDist(type: Copy) {
    from 'src/docs/images'
    into "${project.buildDir}/dist/images"
    include '*.png'
}
task copyHtmlToDist(type: Copy) {
    from "${project.buildDir}/docs/asciidoc"
    into "${project.buildDir}/dist"
    exclude 'common-*.html'
}
task createDist {
    dependsOn('copyImagesToDist')
}
clean.dependsOn('cleanAsciidoctorFolder')
build.dependsOn('asciidoctor')

asciidoctor.dependsOn('generateCodeZip')
asciidoctor.finalizedBy('copyHtmlToDist')
task generateSampleProjects {
    ext {
        metadataConfigName = 'metadata.json'
        output = "$buildDir/code"
    }
    doLast {
        GuideProjectGenerator generator = new GuideProjectGenerator()
        new File("guides").eachDir { dir ->
            File configFile = new File("$dir/$metadataConfigName")
            if (!configFile.exists()) {
                throw new GradleException("metadata file not found for ${dir.name}")
            }
            def config = new JsonSlurper().parse(configFile)
            GuideMetadata metadata = new GuideMetadata(asciidoctor: config.asciidoctor,
                    slug: config.slug,
                    title: config.title,
                    intro: config.intro,
                    features: config.features,
                    authors: config.authors,
                    languages: config.languages ?: ['java', 'groovy', 'kotlin'],
                    buildTools: config.buildTools ?: ['gradle', 'maven'],
                    testFramework: config.testFramework
            )
            generator.generate(metadata, dir, new File(output))
            GuideAsciidocGenerator.generate(metadata, dir, new File('src/docs/asciidoc'))
        }
        generator.close()
    }
    finalizedBy('generateTestScript')
}

task generateTestScript {
    ext {
        excludeDirs = ['images']
        stopIfFailure = false
    }
    doLast {
        File output = new File(generateSampleProjects.output + "/test.sh")
        output.createNewFile()
        String bashScript = '''\
#!/usr/bin/env bash
set -e

FAILED_PROJECTS=()
EXIT_STATUS=0
'''
        new File(generateSampleProjects.output).eachDir {dir ->
            if (!excludeDirs.contains(dir.name)) {
                bashScript += """\

cd ${dir.name}
echo "-------------------------------------------------"
echo "Executing '${dir.name}' tests"
${dir.name.contains('gradle') ? './gradlew test' : './mvnw test' } || EXIT_STATUS=\$?
cd ..
"""
if (stopIfFailure) {
    bashScript += """\
if [ \$EXIT_STATUS -ne 0 ]; then
  echo "'${dir.name}' tests failed => exit \$EXIT_STATUS"
  exit \$EXIT_STATUS
fi
"""
} else {
    bashScript += """\
if [ \$EXIT_STATUS -ne 0 ]; then
  FAILED_PROJECTS=("\${FAILED_PROJECTS[@]}" ${dir.name})
  echo "'${dir.name}' tests failed => exit \$EXIT_STATUS"
fi
EXIT_STATUS=0
"""
}
            }

        }
if (!stopIfFailure) {
    bashScript += '''
if [ ${#FAILED_PROJECTS[@]} -ne 0 ]; then
  echo ""
  echo "-------------------------------------------------"
  echo "Projects with errors:"
  for p in `echo ${FAILED_PROJECTS[@]}`; do
    echo "  $p"
  done;
  echo "-------------------------------------------------"
  exit 1
else
  exit 0
fi

'''
}
        output.text = bashScript
        output.setExecutable(true)
    }
}

task generateCodeZip {
    dependsOn 'generateSampleProjects'
    dependsOn 'createDist'
    doLast {
        new File(generateSampleProjects.output).eachDir { dir ->
            String filename = projectDir.absolutePath + "/build/dist/" + dir.absolutePath.substring(dir.absolutePath.lastIndexOf('/')) + ".zip"
            GenerateGuideZip.compressZipfile(dir.absolutePath, filename)
        }
    }
}
